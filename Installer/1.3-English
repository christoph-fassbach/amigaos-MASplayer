.BRA {
.KET }

FAILAT 10

; *e[30m - blue
; *e[31m - white
; *e[32m - black
; *e[33m - orange

C:VERSION >NIL: exec.library 36
IF NOT WARN
  ECHO "Sie verwenden OS2.0 oder neuer."
  ECHO "Bitte verwenden Sie die OS2.0+ Installationsskripte."
  ECHO "Dankeschön!"
  ENDCLI
ENDIF

RESIDENT C:ECHO
RESIDENT C:ASK
RESIDENT C:ASSIGN
RESIDENT C:IF
RESIDENT C:ENDIF
RESIDENT C:EDIT

ASSIGN MASplayer-13-SRC: ""

ECHO "Welcome to the MASplayer OS 1.3 install script."
ECHO ""
ECHO "If you want to abort, please press CTRL-C at any point."
ECHO "If anything is unclear, please refer to the ReadMeFirst.txt"
ECHO "or check the manual."
ECHO ""
ECHO "+-------------------------------------------------------------------+"
ECHO "|                      Base tools installation                      |"
ECHO "+-------------------------------------------------------------------+"
ECHO ""
ASK "Do you want to install the MASplayer base tools? (y/N)"

IF WARN
  ECHO "The default installation path for the MASplayer base tools is:"
  ECHO "*e[33mSYS:Expansion/MASplayer*e[31m"
  ECHO "However, this may cause issues with ... very well written software."
  ECHO ""
  ASK "Do you want to adapt the path before installing (y/N)?"
 
  IF WARN
    ECHO "Enter your desired installation path and push Return:"
    SETENV >NIL: TOOLSPATH ?
  ELSE
    SETENV >NIL: TOOLSPATH "SYS:Expansion/MASplayer"
  ENDIF

  ECHO   >  RAM:MASplayer_installer1 "IF NOT EXISTS TOOLSPATH"
  ECHO   >> RAM:MASplayer_installer1 "  MAKEDIR TOOLSPATH"
  ECHO   >> RAM:MASplayer_installer1 "ENDIF"
  ECHO   >> RAM:MASplayer_installer1 "COPY MASplayer-13-SRC:Tools/#? TOOLSPATH"
  ECHO   >> RAM:MASplayer_installer1 "COPY MASplayer-13-SRC:Folder.infoV36 TOOLSPATH.info"

  ; Prepare EDIT script to replace TOOLSPATH with correct path in installer script
  ECHO   >  RAM:MASplayer_editscript1 "GE ?TOOLSPATH?" NOLINE
  GETENV >> RAM:MASplayer_editscript1 TOOLSPATH
  ECHO   >> RAM:MASplayer_editscript1 "?" NOLINE

  ; Prepare 2nd EDIT script to put 1st EDIT script into single line... *sigh*
  ECHO   >  RAM:MASplayer_editscript2 "CL //"
  
  ; Run 2nd EDIT script on 1st EDIT script
  EDIT >NIL: FROM RAM:MASplayer_editscript1 TO RAM:MASplayer_editscript3 WITH RAM:MASplayer_editscript2

  ; Run 1st EDIT script on installer script
  EDIT >NIL: FROM RAM:MASplayer_installer1 TO RAM:MASplayer_installer2 WITH RAM:MASplayer_editscript3

  ; Run installer script
  EXECUTE RAM:MASplayer_installer2

  ; Clean that mess up again...
  DELETE >NIL: RAM:MASplayer_editscript#? RAM:MASplayer_installer#?
ENDIF

ECHO ""
ECHO "+-------------------------------------------------------------------+"
ECHO "|                      MHI driver installation                      |"
ECHO "+-------------------------------------------------------------------+"
ECHO ""
ECHO "OS 1.3 is a bit ... picky ... about loading libraries."
ECHO "To make MHI work reliably, the MHI driver needs "
ECHO "to be installed to the default library location - LIBS:."
ECHO ""
ECHO "The most common versions of MASplayer are pro, pro2, pro2+ and"
ECHO "pro2+ with Bluetooth. Therefore, this should be the correct driver"
ECHO "version for your MASplayer."
ECHO ""
ASK "Do you want to install mhimaspro.library to LIBS:? (y/N)"

IF WARN
  ECHO "Which MHI driver do you want to install?"
  ECHO ""
  ECHO "1) 68000, RELEASE version, no logging, RECOMMENDED!!!"
  ECHO "2) 68000, DEBUG version, serial logging"
  ECHO "5) 68020, RELEASE version, no logging, use this if on 68020+"
  ECHO "6) 68020, DEBUG version, serial logging, for 68020+"
  ECHO ""
  ECHO "*e[33mDefault: 1*e[31m - please enter 1-8 and push Return:"
  SETENV >NIL: mhi_variant ?

  IF $mhi_variant EQ 2
    COPY MHI/mhimaspro.library-000-SER_LOG LIBS:mhimaspro.library CLONE
    ECHO "Option 2 copied..."
  ELSE
    IF $mhi_variant EQ 3
      COPY MHI/mhimaspro.library-000-FILE_LOG LIBS:mhimaspro.library CLONE
      ECHO "Option 3 copied..."
    ELSE
      IF $mhi_variant EQ 4
        COPY MHI/mhimaspro.library-000-MEM_LOG LIBS:mhimaspro.library CLONE
        ECHO "Option 4 copied..."
      ELSE
        IF $mhi_variant EQ 5
          COPY MHI/mhimaspro.library-020-NO_LOG LIBS:mhimaspro.library CLONE
          ECHO "Option 5 copied..."
        ELSE
          IF $mhi_variant EQ 6
            COPY MHI/mhimaspro.library-020-SER_LOG LIBS:mhimaspro.library CLONE
            ECHO "Option 6 copied..."
          ELSE
            IF $mhi_variant EQ 7
              COPY MHI/mhimaspro.library-020-FILE_LOG LIBS:mhimaspro.library CLONE
              ECHO "Option 7 copied..."
            ELSE
              IF $mhi_variant EQ 8
                COPY MHI/mhimaspro.library-020-MEM_LOG LIBS:mhimaspro.library CLONE
                ECHO "Option 8 copied..."
              ELSE
                COPY MHI/mhimaspro.library-000-NO_LOG LIBS:mhimaspro.library CLONE
                ECHO "Option 1 copied..."
              ENDIF
            ENDIF
          ENDIF
        ENDIF
      ENDIF
    ENDIF
  ENDIF
ENDIF

ECHO ""
ECHO "Do you want to copy all variants of mhimaspro.library to LIBS: for"
ASK "potential later use after renaming? (y/N)"

IF WARN
  copy MHI/mhimaspro.library#? LIBS:
ENDIF

ECHO ""
ECHO "In less than 1% of the cases, this will be the correct driver"
ECHO "version. Please only install it, if you are perfectly sure you are"
ECHO " using a MASplayer Standard."
ECHO ""
ASK "Do you want to install mhimasstd.library to LIBS:? (y/N)"

IF WARN
  ECHO "Which MHI driver do you want to install?"
  ECHO ""
  ECHO "1) 68000, RELEASE version, no logging, RECOMMENDED!!!"
  ECHO "2) 68000, DEBUG version, serial logging"
  ECHO "5) 68020, RELEASE version, no logging, use this if on 68020+"
  ECHO "6) 68020, DEBUG version, serial logging, for 68020+"
  ECHO ""
  ECHO "*e[33mDefault: 1*e[31m - please enter 1-8 and push Return:"
  SETENV >NIL: mhi_variant ?

  IF $mhi_variant EQ 2
    COPY MHI/mhimasstd.library-000-SER_LOG LIBS:mhimasstd.library CLONE
    ECHO "Option 2 kopiert..."
  ELSE
    IF $mhi_variant EQ 3
      COPY MHI/mhimasstd.library-000-FILE_LOG LIBS:mhimasstd.library CLONE
      ECHO "Option 3 kopiert..."
    ELSE
      IF $mhi_variant EQ 4
        COPY MHI/mhimasstd.library-000-MEM_LOG LIBS:mhimasstd.library CLONE
        ECHO "Option 4 kopiert..."
      ELSE
        IF $mhi_variant EQ 5
          COPY MHI/mhimasstd.library-020-NO_LOG LIBS:mhimasstd.library CLONE
          ECHO "Option 5 kopiert..."
        ELSE
          IF $mhi_variant EQ 6
            COPY MHI/mhimasstd.library-020-SER_LOG LIBS:mhimasstd.library CLONE
            ECHO "Option 6 kopiert..."
          ELSE
            IF $mhi_variant EQ 7
              COPY MHI/mhimasstd.library-020-FILE_LOG LIBS:mhimasstd.library CLONE
              ECHO "Option 7 kopiert..."
            ELSE
              IF $mhi_variant EQ 8
                COPY MHI/mhimasstd.library-020-MEM_LOG LIBS:mhimasstd.library CLONE
                ECHO "Option 8 kopiert..."
              ELSE
                COPY MHI/mhimasstd.library-000-NO_LOG LIBS:mhimasstd.library CLONE
                ECHO "Option 1 kopiert..."
              ENDIF
            ENDIF
          ENDIF
        ENDIF
      ENDIF
    ENDIF
  ENDIF
ENDIF

ECHO ""
ECHO "Do you want to copy all variants of mhimasstd.library to LIBS: for"
ASK "potential later use after renaming? (y/N)"

IF WARN
  copy MHI/mhimasstd.library#? LIBS:
ENDIF

ECHO ""
ECHO "+-------------------------------------------------------------------+"
ECHO "|                         AmigaGuide manual                         |"
ECHO "+-------------------------------------------------------------------+"
ECHO ""

ASK "Do you want to install the MASplayer AmigaGuide manual? (y/N)"
IF WARN
  ECHO "The default installation path for the MASplayer manual is:"
  ECHO "*e[33mSYS:Expansion/MASplayer*e[31m"
  ECHO "Well, you know the story already most likely..."
  ECHO ""
  ASK "Do you want to adapt the path before installing (y/N)?"

  IF WARN
    ECHO "Enter your desired installation path and push Return:"
    SETENV >NIL: TOOLSPATH ?
  ELSE
    SETENV >NIL: TOOLSPATH "SYS:Expansion/MASplayer"
  ENDIF

  ASSIGN >NIL: MASplayer-Manual:

  IF EXISTS MASplayer-13-SRC:Manual
    ; Installing from lha archive
    SETENV >NIL: MANUALPATH "MASplayer-13-SRC:Manual/"
  ELSE
    ; Installing from ADF/Disk
    SETENV >NIL: MANUALPATH "MASplayer-Manual:"
  ENDIF

  ECHO   >  RAM:MASplayer_installer1 "IF NOT EXISTS TOOLSPATH"
  ECHO   >> RAM:MASplayer_installer1 "  MAKEDIR TOOLSPATH"
  ECHO   >> RAM:MASplayer_installer1 "ENDIF"
  ECHO   >> RAM:MASplayer_installer1 "COPY MANUALPATHilbm TOOLSPATH/ilbm"
  ECHO   >> RAM:MASplayer_installer1 "COPY MANUALPATHMASplayer.guide TOOLSPATH/MASplayer.guide"
  ECHO   >> RAM:MASplayer_installer1 "COPY MANUALPATHMASplayer.guide.infoV38 TOOLSPATH/MASplayer.guide.info"
  ECHO   >> RAM:MASplayer_installer1 "COPY MANUALPATHMASplayerv38.guide TOOLSPATH/MASplayerv38.guide"
  ECHO   >> RAM:MASplayer_installer1 "COPY MASplayer-13-SRC:Folder.infoV36 TOOLSPATH.info"

  ; Prepare EDIT script to replace MANUALPATH with correct path in installer script
  ECHO   >  RAM:MASplayer_editscript1 "GE ?MANUALPATH?" NOLINE
  GETENV >> RAM:MASplayer_editscript1 MANUALPATH
  ECHO   >> RAM:MASplayer_editscript1 "?" NOLINE

  ; Prepare EDIT script to replace TOOLSPATH with correct path in installer script
  ECHO   >  RAM:MASplayer_editscript2 "GE ?TOOLSPATH?" NOLINE
  GETENV >> RAM:MASplayer_editscript2 TOOLSPATH
  ECHO   >> RAM:MASplayer_editscript2 "?" NOLINE

  ; Prepare 2nd EDIT script to put 1st + 2nd EDIT script into single line... *sigh*
  ECHO   >  RAM:MASplayer_editscript3 "CL //"
  
  ; Run 2nd EDIT script on 1st EDIT script
  EDIT >NIL: FROM RAM:MASplayer_editscript1 TO RAM:MASplayer_editscript4 WITH RAM:MASplayer_editscript3

  ; Run 2nd EDIT script on 1st EDIT script
  EDIT >NIL: FROM RAM:MASplayer_editscript2 TO RAM:MASplayer_editscript5 WITH RAM:MASplayer_editscript3

  ; Run 1st EDIT script on installer script
  EDIT >NIL: FROM RAM:MASplayer_installer1 TO RAM:MASplayer_installer2 WITH RAM:MASplayer_editscript4

  ; Run 2nd EDIT script on installer script
  EDIT >NIL: FROM RAM:MASplayer_installer2 TO RAM:MASplayer_installer3 WITH RAM:MASplayer_editscript5

  ; Run installer script
  EXECUTE RAM:MASplayer_installer3

  ; Clean that mess up again...
  DELETE >NIL: RAM:MASplayer_editscript#? RAM:MASplayer_installer#?

ENDIF

ECHO ""
ECHO "+-------------------------------------------------------------------+"
ECHO "|                    All done - have a nice day!                    |"
ECHO "+-------------------------------------------------------------------+"

ENDCLI
