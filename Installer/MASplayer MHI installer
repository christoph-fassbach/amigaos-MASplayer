(complete 0)
(delopts "oknodelete" "force" "askuser")

;==============================================================================
; English strings

(set default_lang 4)

(set #which_drivers_target
(cat "Drivers will need to be installed to the corresponding system folder, "
     "LIBS:.\n"
     "In which system disk shall the MASplayer MHI driver be installed?\n"
))

(set #which_drivers_target_help
(cat "\nThis section lets you choose in which drawer or on which disk the "
     "MASplayer MHI driver files should be installed. These should normally go "
     "in the same location as the current Workbench, as that is where MHI "
     "would expect them.\n\n"
     @askdir-help
))

(set #which_tools_target
(cat "The MASplayer MHI tools can be installed wherever you like them to be - "
     "so please select your preferred location.\n"
))

(set #which_tools_target_help
(cat "\nThis section lets you choose in which drawer or on which disk the "
     "MASplayer MHI tools should be installed. It can be anywhere you like.\n\n"
     "Default is Sys:Expansion/MASplayer - which may cause issues with some "
     "obscure Commodore expansions (bridgeboards) and their drivers.\n\n"
     @askdir-help
))

(set #which_cpu
(cat "Please select which CPU version of the %s driver shall be installed.\n\n"
))

(set #which_cpu_help
(cat "\nThis section lets you choose which CPU-optimized version of the %s "
     "driver shall be installed. If in doubt, please select 68000, you may "
     "lose a little performance, but avoid crashes. \n\n"
))

(set #which_debug_driver
(cat "Do you want to use a driver with debug output enabled? Please only do "
     "so if you know what you are doing or were asked to do so.\n"
     "(WARNING: SLOWER!)\n\n"
))

(set #which_debug_driver_help
(cat "\nDebug versions produce log output with different targets, "
     "the Amiga's serial port, a file, or just into some memory area. "
     "All of these options are slower than the release version, "
     "may randomly crash or destroy your hard disk. "
     "Please do not play around here, if you are unsure.\n\n"
     "You have been warned!"
))

(set #release_driver
(cat "No - release is fine (RECOMMENDED)"
))

(set #serial_debug_driver
(cat "Yes - serial debug output"
))

(set #file_debug_driver
(cat "Yes - file logging"
))

(set #memory_debug_driver
(cat "Yes - memory logging"
))

(set #all_versions
(cat "Do you want to copy all %s driver versions next to the active one for "
     "later potential use?"
))

(set #all_versions_help
(cat "\nAll available versions of the %s driver would be copied next to the "
     "activated one in case you want to switch later.\n"
     "This includes all CPU and debug variants."
))

(set #yes
(cat "Yes"
))

(set #no
(cat "No"
))

(set #what_components
(cat "Please select what should be installed."
))

(set #what_components_help
(cat "\nThis section lets you choose which parts of the MASplayer MHI "
     "package shall be installed. All of them should work independently "
     "from each other.\n"
     "\n"
     "MHI driver for MASplayer Pro - Installs the MASplayer Pro drivers for MHI, a standard for "
     "hardware accelerated playback of encoded audio files like MP3. "
     "Recommended, as that is the core of the package.\n"
     "MHI driver for MASplayer Std - Same as above, but for MASplayer Std.\n"
     "MHIplay - Command line tool to play back MP3s or other encoded files.\n"
     "Test - Command line tool to test the MHI driver is loaded correctly.\n"
     "Debug Tools - Tools to help debug MHI drivers when needed.\n"
     "Manual - MASplayer MHI's AmigaGuide manual. Recommended.\n"
     "\n"
))

(set #component_mhi_pro
(cat "MHI driver for MASplayer Pro"
))

(set #component_mhi_std
(cat "MHI driver for MASplayer Std"
))

(set #component_play
(cat "MHIplay"
))

(set #component_test
(cat "Test"
))

(set #component_debug
(cat "Debug Tools"
))

(set #component_manual
(cat "Manual"
))

(set #which_manual_target
(cat "Where shall the MASplayer AmigaGuide manual be installed?\n"
))

(set #which_manual_target_help
(cat "\nThe MASplayer AmigaGuide manual consists of the actual AmigaGuide file "
     "and a folder with the embedded or linked images. You can choose the "
     "location for both here.\n\n"
     "Default is Sys:Expansion/MASplayer - which may cause issues with some "
     "obscure Commodore expansions (bridgeboards) and their drivers.\n\n"
     @askdir-help
))


;==============================================================================
; German strings

(if (= @language "deutsch")
(
(set default_lang 2)

(set #which_drivers_target
(cat "Die Treiber werden in die passenden Systemverzeichnisse installiert, "
     "LIBS:.\n"
     "Auf welches Systemlaufwerk sollen die MASplayer MHI Treiber installiert "
     "werden?\n"
))

(set #which_drivers_target_help
(cat "\nIn diesem Teil können Sie wählen, auf welche Laufwerk oder "
     "in welches Verzeichnis die MASplayer MHI Treiber installiert werden sollen. "
     "Sie sollten normalerweise an dieselbe Stelle installiert werden "
     "wie die aktuelle Workbench, weil das ist, wo MHI sie "
     "erwarten würde.\n\n"
     @askdir-help
))

(set #which_tools_target
(cat "Die MASplayer Basiswerkzeuge können an einen beliebigen Ort installiert "
     "werden - suchen Sie bitte ihren bevorzugten Ort aus.\n"
))

(set #which_tools_target_help
(cat "\nIn diesem Teil können Sie wählen, auf welchem Laufwerk oder "
     "Verzeichnis die MASplayer Basiswerkzeuge installiert werden sollen. "
     "Der Ort ist dabei absolut beliebig. \n\n"
     "Voreinstellung ist Sys:Expansion/MASplayer - was Probleme mit einigen "
     "obskuren Commodore Erweiterungen (bridgeboards) und deren Treibern "
     "verursachen kann.\n\n"
     @askdir-help
))

(set #which_cpu
(cat "Bitte wählen Sie aus, welche CPU-optimierte Version des %s Treibers "
     "installiert werden soll.\n\n"
))

(set #which_cpu_help
(cat "\nIn diesem Teil können Sie wählen, für welche CPU die optimierte %s "
     "Treiberversion installiert werden soll. Im Zweifelsfall wählen Sie "
     "bitte 68000. Sie verlieren möglicherweise etwas Leistung, aber "
     "vermeiden Abstürze. \n\n"
))

(set #which_debug_driver
(cat "Möchten Sie einen Treiber mit debug-Ausgabe verwenden? "
     "Bitte wählen Sie diese nur, wenn Sie wissen, was sie tun oder darum "
     "gebeten wurden.\n"
     "(WARNUNG: LANGSAMER!)\n\n"
))

(set #which_debug_driver_help
(cat "\nDebugversionen produzieren log-Ausgaben mit verschiedenen Zielen, "
     "den seriellen Port des Amiga, eine Datei, oder einfach einen "
     "Speicherbereich. Alle diese Optionen sind langsamer als die Version "
     "ohne Logging, können zufällig abstürzen oder die Festplatte "
     "beschädigen. Bitte spielen Sie hier nicht herum, wenn sie unsicher "
     "sind, was das bedeutet.\n\n"
     "Sie wurden gewarnt!"
))

(set #release_driver
(cat "Nein - release ist besser (RECOMMENDED)"
))

(set #serial_debug_driver
(cat "Ja - serielle debug-Ausgabe"
))

(set #file_debug_driver
(cat "Ja - debug-Ausgabe in eine Datei"
))

(set #memory_debug_driver
(cat "Ja - debug-Ausgabe im Speicher"
))

(set #all_versions
(cat "Möchten Sie alle %s Treiber Versionen neben dem Aktiven ablegen, falls "
     "Sie ihn später mal brauchen?"
))

(set #all_versions_help
(cat "\nAlle verfügbaren Versionen des %s Treibers würden neben dem Aktiven "
     "abgelegt werden, um einen schnellen und einfachen Wechsel zwischen den "
     "Treibern zu ermöglichen.\n"
     "Dies umfasst alle CPU- und Debug-Varianten."
))

(set #yes
(cat "Ja"
))

(set #no
(cat "Nein"
))

(set #what_components
(cat "Bitte die zu installierenden Komponenten auswählen."
))

(set #what_components_help
(cat "\nIn diesem Abschnitt können sie auswählen, welche Teile des MASplayer MHI"
     "Softwarepakets installiert werden sollen. Alle Teile sollten unabhängig "
     "voneinander funktionieren.\n"
     "\n"
     "MHI Treiber für MASplayer Pro - Installiert die MASplayer Pro Treiber für MHI, einen Standard "
     "für hardwarebeschleunigte Wiedergabe von komprimierten Audiodateien "
     "wie MP3. Empfohlen, dis ist der Kern des Pakets.\n"
     "MHI Treiber für MASplayer Std - Siehe oben, aber für den MASplayer Std.\n"
     "MHIplay - Kommandozeilentool, um MP3s oder andere encodierte Dateien abzuspielen.\n"
     "Test - Kommandozeilentool, um zu testen, dass der MHI Treiber korrekt geladen wurde.\n"
     "Debugwerkzeuge - Verschiedene Werkzeuge, um beim debuggen der MHI Treiber zu helfen, wenn nötig.\n"
     "Anleitung - MASplayer MHI's AmigaGuide Benutzerhandbuch. Empfohlen.\n"
     "\n"
))

(set #component_mhi_pro
(cat "MHI Treiber für MASplayer Pro"
))

(set #component_mhi_std
(cat "MHI Treiber für MASplayer Std"
))

(set #component_play
(cat "MHIplay"
))

(set #component_test
(cat "Test"
))

(set #component_debug
(cat "Debugwerkzeuge"
))

(set #component_manual
(cat "Anleitung"
))

(set #which_manual_target
(cat "Wo soll das MASplayer AmigaGuide Handbuch installiert werden?\n"
))

(set #which_manual_target_help
(cat "\nDas MASplayer AmigaGuide Handbuch besteht aus der eigentlichen "
     "AmigaGuide-Datei und einem Verzeichnis mit den eingebetteten oder "
     "verlinkten Bildern. Hier können Sie den Ort für beides auswählen.\n\n"
     "Voreinstellung ist Sys:Expansion/MASplayer - was Probleme mit einigen "
     "obskuren Commodore Erweiterungen (bridgeboards) und deren Treibern "
     "verursachen kann.\n\n"
     @askdir-help
))

))

;=============================================================================
(set cpu_found (database "cpu"))
(if (OR (= cpu_found "68000") (= cpu_found "68010"))
  (set cpu_default 0)
  (if (OR (= cpu_found "68020") (= cpu_found "68030"))
      (set cpu_default 1)
      (set cpu_default 2)
  )
)

(procedure install_driver (
  (set install_driver_cpu cpu_default)
  (set install_driver_debug 0)
  (set install_driver_all 0)

  (if
    (>= @user-level 1)
    (
      (set install_driver_cpu 
        (askchoice
          (prompt (#which_cpu driver_name))
          (help (#which_cpu_help driver_name))
          (choices "68000" "68020" "68040")
          (default install_driver_cpu)
        )
      )

      (set install_driver_debug
        (askchoice
          (prompt #which_debug_driver)
          (help #which_debug_driver_help)
          (choices
            #release_driver
            #serial_debug_driver
;            #file_debug_driver
;            #memory_debug_driver
          )
          (default install_driver_debug)
        )
      )
    )
  )

  (if
    (>= @user-level 2)
    (set install_driver_all
      (askbool
        (prompt (#all_versions driver_name))
        (help (#all_versions_help driver_name))
        (choices #yes #no)
        (default install_driver_all)
      )
    )
  )

  (set install_driver_cpu (select install_driver_cpu "-000" "-020" "-040"))
  (set install_driver_debug (select install_driver_debug "-NO_LOG" "-SER_LOG"
;  "-FILE_LOG" "-MEM_LOG"
  ))
))

;=============================================================================
; say hi!
(welcome)
(set old_level @user-level)

(onerror (TERMINATE))

(set what
  (askoptions 
    (prompt #what_components)
    (help #what_components_help)
    (choices #component_mhi_pro
             #component_mhi_std
             #component_play
             #component_test
             #component_manual )
    (default %10101)
  )
)

(if
  (BITAND what %00011)
  (
    (set drivers_target
      (askdir
        (prompt #which_drivers_target)
        (help #which_drivers_target_help)
        (default "SYS:")
      )
    )
  )
)

(if
  (BITAND what %00001)
  (
    (set driver_name "MHI")
    (install_driver)

    (set source_folder "MHI")
    (set file "mhimaspro.library")
    (set target_folder (tackon drivers_target "Libs/MHI"))
    (set source_file (tackon source_folder file))

    (makedir target_folder)
    (copyfiles
      (source (cat source_file install_driver_cpu install_driver_debug))
      (newname file)
      (dest target_folder)
    )
    
    (if install_driver_all
      (copyfiles
        (source source_folder)
        (pattern (cat file "#?"))
        (dest target_folder)
      )
    )
  )
)

(if
  (BITAND what %00010)
  (
    (set driver_name "MHI")
    (install_driver)

    (set source_folder "MHI")
    (set file "mhimasstd.library")
    (set target_folder (tackon drivers_target "Libs/MHI"))
    (set source_file (tackon source_folder file))

    (makedir target_folder)
    (copyfiles
      (source (cat source_file install_driver_cpu install_driver_debug))
      (newname file)
      (dest target_folder)
    )
    
    (if install_driver_all
      (copyfiles
        (source source_folder)
        (pattern (cat file "#?"))
        (dest target_folder)
      )
    )
  )
)

(complete 25)

(set tools_target "SYS:Expansion/MASplayer")
(if
  (BITAND what %01100)
  (
    (set tools_target
      (askdir (prompt #which_tools_target)
        (help #which_tools_target_help)
        (default tools_target)
        (newpath)
      )
    )
    (makedir tools_target (infos))
    (set source_folder "Tools")
  )
)

(complete 30)

(if
  (BITAND what %00100)
  (copyfiles
    (source (tackon source_folder "MHIplay"))
    (dest tools_target)
    (infos)
  )
)

(complete 40)

(if
  (BITAND what %01000)
  (copyfiles
    (source (tackon source_folder "Test"))
    (dest tools_target)
    (infos)
  )
)

(complete 50)

(if
  (BITAND what %10000)
  (
    (set source_folder "Manual")
    (set manual_target
      (askdir (prompt #which_manual_target)
        (help #which_manual_target_help)
        (default tools_target)
        (newpath)
      )
    )
    (makedir manual_target (infos))
    (copyfiles
      (source source_folder)
      (pattern "#?.guide")
      (dest manual_target)
      (infos)
    )
    (copyfiles
      (source (tackon source_folder "ilbm"))
      (dest (tackon manual_target "ilbm"))
      (all)
    )
  )
)

(complete 100)

